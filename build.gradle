buildscript {
    repositories {
        maven { url 'http://repo.lapis.blue/releases' }
    }
}

// Apply Gradle plugins
plugins {
    id 'java'
    id 'eclipse'
    id 'idea'

    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

defaultTasks 'clean', 'build', 'fatJar'

// Project information
group = 'me.escapeNT.pail'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 1.6
targetCompatibility = 1.6

// Project repositories
repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' 
    }
    // Add Sponge repo for custom checkstyle implementation to fix some wrong warnings
    maven { name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven' 
    }
}

configurations {
    shadow
    compile {
        extendsFrom shadow
    }
    fatt
}

// Project dependencies
dependencies {
    fatt 'com.jtattoo:JTattoo:1.6.11'
    
    compile 'org.spongepowered:spongeapi:2.0'
    shadow('org.slf4j:jul-to-slf4j:1.7.7') {
        transitive = false
    }

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testRuntime 'org.slf4j:slf4j-simple:1.7.7'
}

// Read source files using UTF-8
tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-deprecation']
    options.encoding = 'UTF-8'
}


test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        events 'skipped'
    }
}

jar { // The Pore classes will be created in Pore-VERSION-base.jar first
    classifier = 'base'
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Lamp-Post',
                'Main-Class': 'me.escapeNT.pail.Main'
        )
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Lamp-Post',
                'Main-Class': 'me.escapeNT.pail.Main'
        )
    }
    from { configurations.fatt.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

shadowJar {
    configurations = [project.configurations.shadow]
    dependencies {
        exclude dependency('com.google.guava:guava')
        exclude dependency('com.google.code.gson:gson')
    }

    // Relocate jul-to-slf4j
    relocate 'org.slf4j.bridge', 'me.escapeNT.pail.org.slf4j.bridge'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
